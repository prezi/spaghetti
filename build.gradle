def gradleVer = "2.0"
def groovyVersion = "2.3.6"

String gitVersion
if (hasProperty("release")) {
	gitVersion = [ "git", "describe", "--match", "[0-9]*", "--dirty"].execute().text.trim()
} else {
	gitVersion = [ "git", "describe", "--match", "[0-9]*", "--abbrev=0"].execute().text.trim() + "-SNAPSHOT"
}

task version << {
	println "Version: ${gitVersion}"
}

def sonatype = hasProperty("sonatype")
def signArtifacts = sonatype || hasProperty("sign")

allprojects {
	group = 'com.prezi.spaghetti'
	description = "Spaghetti"
	version = gitVersion
	apply plugin: 'idea'

	tasks.withType(Upload).all { dependsOn "check" }
	tasks.withType(org.gradle.api.tasks.compile.JavaCompile) {
		options.compilerArgs << "-Xlint:unchecked"
	}
}

subprojects { subproject ->
	apply plugin: 'maven'
	// For some reason this is needed ot have dependencies in POM files
	apply plugin: "java"

	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "http://gradle.artifactoryonline.com/gradle/libs"
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				pom.project {
					url "http://github.com/prezi/spaghetti/"
					name subproject.name
					description subproject.description

					scm {
						url "http://github.com:prezi/spaghetti"
						connection "scm:git@github.com:prezi/spaghetti.git"
						developerConnection "scm:git@github.com:prezi/spaghetti.git"
					}

					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/licenses/LICENSE-2.0.txt"
							distribution "repo"
						}
					}

					developers {
						developer {
							id "lptr"
							name "Lorant Pinter"
						}
						developer {
							id "exFalso"
							name "Andras Slemmer"
						}
						developer {
							id "gidapataki"
							name "Gida Pataki"
						}
					}
				}

				if (sonatype) {
					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						def user = project.getProperty("sonatypeUsername")
						def password = project.getProperty("sonatypePassword")
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
						def user = project.getProperty("sonatypeUsername")
						def password = project.getProperty("sonatypePassword")
						authentication(userName: user, password: password)
					}
				} else if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
					def user = project.getProperty("nexusUser")
					def password = project.getProperty("nexusPassword")
					repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
						authentication(userName: user, password: password)
					}
				}
			}
		}
	}

	if (signArtifacts) {
		apply plugin: "signing"

		signing {
			sign configurations.archives
		}

		uploadArchives {
			repositories {
				mavenDeployer {
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				}
			}
		}
	}
}

ext.codeProjects = subprojects.findAll {
			it.name == "spaghetti-core" || it.name ==~ /gradle-.*/ || it.name ==~ /spaghetti-.*-support/
		}
ext.groovyProjects = [ project(":spaghetti-haxe-support"), project(":spaghetti-kotlin-support"), project(":spaghetti-typescript-support") ]
ext.javaProjects = codeProjects - groovyProjects

configure(subprojects.findAll { it.name != "spaghetti-docs" }) {
	apply plugin: 'groovy'

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"

	tasks.withType(AbstractCompile) {
		options.encoding = "UTF-8"
	}

	dependencies {
		testCompile "junit:junit:4.11"
		testCompile "org.mockito:mockito-all:1.9.5"
		testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
			exclude group: "org.codehaus.groovy"
		}
		testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = "sources"
	}

	jar {
		from rootProject.file("LICENSE.txt")
	}

	def doc
	if (project.name ==~ /spaghetti-(haxe|typescript)-support/) {
		doc = groovydoc
	} else {
		doc = javadoc
	}

	task docJar(type: Jar) {
		dependsOn doc
		classifier = "javadoc"
		from doc.destinationDir
	}

	artifacts {
		archives jar
		archives docJar
		archives sourcesJar
	}
}

subprojects {
	configurations {
		groovydoc { extendsFrom compile }
	}

	dependencies {
		groovydoc "org.codehaus.groovy:groovy-all:${groovyVersion}"
	}

	String[] apis = [
		"http://docs.oracle.com/javase/8/docs/api/",
		"http://groovy.codehaus.org/gapi/",
		"http://www.gradle.org/docs/${gradleVer}/javadoc/",
		// "http://prezi.github.io/gradle-haxe-plugin/javadoc/",
		// "http://prezi.github.io/gradle-typescript-plugin/javadoc/",
	]

	def excludes = [
		"**/internal/*",
	]

	tasks.withType(Javadoc) {
		options.encoding = "UTF-8"
		options.links apis
		options.noTimestamp = true
		exclude excludes
	}

	tasks.withType(Groovydoc) {
		groovydoc.groovyClasspath = configurations.groovydoc
		// Done in doFirst() so that we don't download stuff in configuration phase
		doFirst {
			apis.each {
				link(it, *(new URL("$it/package-list").text.tokenize("\n")))
			}
		}
		exclude "**/internal/**"
		exclude excludes
	}
}

def inheritJavadoc(Project target, Project dependency) {
	def coreJavadoc = dependency.tasks.javadoc
	def javadocUrl = "http://prezi.github.io/spaghetti/javadoc/"

	target.javadoc {
		dependsOn coreJavadoc
		options.linksOffline javadocUrl, coreJavadoc.destinationDir.path
	}

	target.groovydoc {
		dependsOn coreJavadoc
		doFirst {
			link(javadocUrl, *(new File(coreJavadoc.destinationDir, "package-list").text.tokenize("\n")))
		}
	}
}

// Make Javadocs depend on core
configure(codeProjects - project(":spaghetti-core")) { p ->
	inheritJavadoc p, project(":spaghetti-core")
}

configure(subprojects.findAll { it.name ==~ /gradle-spaghetti-.*-plugin/}) { p ->
	inheritJavadoc p, project(":gradle-spaghetti-plugin")
}

project(":spaghetti-core") {
	dependencies {
		compile "com.google.javascript:closure-compiler:v20140814"
	}
}

configure(groovyProjects) {
	dependencies {
		compile "org.codehaus.groovy:groovy:${groovyVersion}"
	}
}

configure(subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }) {
	dependencies {
		compile project(path: ":spaghetti-core")
		testCompile project(path: ":spaghetti-core", configuration: "testCompile")
	}
}

configure(project(":spaghetti")) {
	apply plugin: "application"

	dependencies {
		compile project(":spaghetti-core")
		compile "io.airlift:airline:0.6"
		compile "ch.qos.logback:logback-classic:1.1.2"
		runtime rootProject.subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }
	}

	mainClassName = "com.prezi.spaghetti.cli.SpaghettiCli"

	build { dependsOn installApp }

	configure(applicationDistribution) {
		from(rootProject.file("README.md")) {
			into "/"
		}
		from(rootProject.file("LICENSE.txt")) {
			into "/"
		}
	}

	artifacts {
		archives(distZip) {
			classifier "dist"
		}
	}
}

project(":gradle-spaghetti-plugin") {
	dependencies {
		compile gradleApi()
		compile project(path: ":spaghetti-core")

		// Required only for the incubating stuff brought over from Gradle 2.0
		compile "commons-lang:commons-lang:2.6"
		compile "commons-io:commons-io:2.4"

		// Add all support projects
		compile rootProject.subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }
	}

	task run(type: Exec) {
		// Make sure we install all dependencies
		dependsOn install
		configurations.runtime.allDependencies.withType(ProjectDependency).all { ProjectDependency projectDep ->
			dependsOn projectDep.dependencyProject.getTasksByName("install", false)
		}
		workingDir "src/test/at"
		commandLine = ["gradle", "clean", "packApplication", "-is", "--no-daemon"]
	}
}

configure(subprojects.findAll { it.name ==~ /gradle-spaghetti-.*-plugin/}) {
	dependencies {
		compile gradleApi()
		compile project(path: ":gradle-spaghetti-plugin")
	}
}

project(":gradle-spaghetti-haxe-plugin") {
	dependencies {
		compile "com.prezi.haxe:gradle-haxe-plugin:4.1.2"
		compile "org.webjars:requirejs:2.1.8"
	}
}

project(":gradle-spaghetti-typescript-plugin") {
	dependencies {
		compile "com.prezi.typescript:gradle-typescript-plugin:2.2.2"
	}
}

project(":gradle-spaghetti-kotlin-plugin") {
	dependencies {
		compile "org.jetbrains.kotlin:kotlin-gradle-plugin:0.9.66"
		compile "org.jetbrains.kotlin:kotlin-gradle-plugin-core:0.9.66"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleVer
}
